PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX Person: <http://hospital.example/DB/Person#>
PREFIX Sex_DE: <http://hospital.example/DB/Sex_DE#>
PREFIX Item_Medication: <http://hospital.example/DB/Item_Medication#>
PREFIX Medication: <http://hospital.example/DB/Medication#>
PREFIX Medication_DE: <http://hospital.example/DB/Medication_DE#>
PREFIX NDCcodes: <http://hospital.example/DB/NDCcodes#>

PREFIX hl7: <http://www.hl7.org/v3ballot/xml/infrastructure/vocabulary/vocabulary#>
PREFIX spl: <http://www.hl7.org/v3ballot/xml/infrastructure/vocabulary/vocabulary#>

CONSTRUCT {
?person
    a					  hl7:Person ;
    hl7:entityName			  ?middleName ;
    hl7:livingSubjectBirthTime		  ?dob ;
    hl7:administrativeGenderCodePrintName ?sex ;
    hl7:substanceAdministration		  [
	a	       hl7:SubstanceAdministration ;
 	hl7:consumable [
	    hl7:displayName	 ?takes ;
	    spl:activeIngredient [
		spl:classCode ?ingred
	    ]
	] ;
	hl7:effectiveTime [
	    hl7:start ?indicDate
	    # hl7:end   (?indicDate + ?indicDuration)
	]
    ] .
} WHERE {
  ?person     Person:MiddleName		     ?middleName ;
              Person:DateOfBirth	     ?dob ;
              Person:SexDE		     ?sexEntry   .

  OPTIONAL {
              ?sexEntry   Sex_DE:EntryName   ?sex .
  }

  OPTIONAL {
  ?indicItem  Item_Medication:PatientID	     ?person ;
              Item_Medication:PerformedDTTM  ?indicDate ;
              Item_Medication:EntryName	     ?takes .
  ?indicMed   Medication:ItemID		     ?indicItem ;
              Medication:DaysToTake	     ?indicDuration ;
              Medication:MedDictDE	     ?indicDE .
  ?indicDE    Medication_DE:NDC		     ?indicNDC .
  }

  OPTIONAL {
  ?indicCode  NDCcodes:NDC         ?indicNDC ;
              NDCcodes:ingredient  ?ingred .
  }
}
